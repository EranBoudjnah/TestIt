import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext.kotlin_version = '1.7.10'
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.2.1"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion kotlin_version
        }
        if (requested.group == 'com.pinterest' && requested.name == 'ktlint') {
            details.useVersion '0.45.2'
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

tasks.register("installGitHook", Copy) {
    def suffix = "macos"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        suffix = "windows"
    }
    from new File(rootProject.rootDir, "automation/scripts/pre-commit-$suffix")
    into { new File(rootProject.rootDir, '.git/hooks') }
    rename("pre-commit-$suffix", 'pre-commit')
    fileMode 0775
}

tasks.register("installCodeStyle", Copy) {
    from new File(rootProject.rootDir, 'automation/codeStyles/Project.xml')
    into { new File(rootProject.rootDir, '.idea/codeStyles') }
}

tasks.getByPath(':app:compileKotlin').dependsOn installGitHook, installCodeStyle

apply plugin: "org.jlleitschuh.gradle.ktlint"

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
}
